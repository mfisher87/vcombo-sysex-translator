desc: Translate VR-730 SysEx outputs to MIDI CC. Control your VSTs with your keyboard like you'd expect.

@init
sysex_status_byte=0xF0;
cc_status_byte=0xB0;

drawbar_scale_factor=16;
output_channel=0x00;
drawbar_cc_start_num=0x0C;

@block
buf = 10000;
maxlen = 65536;
while ((recvlen = midirecv_buf(offset,buf,maxlen)) > 0) (
    b01_status_byte=buf[0];
    b08_control_index=buf[7];
    b09_control_val=buf[8];
    b10_checksum=buf[9];
    
    // SysEx:
    b01_status_byte == sysex_status_byte ? (
        // Drawbars:
        b08_control_index>=3 && b08_control_index<=11 ? (
            drawbar_index = b08_control_index-3;
            drawbar_value = b09_control_val * drawbar_scale_factor;
            drawbar_value > 127 ? (
              drawbar_value = 127;
            );
            midisend(
                offset,
                cc_status_byte + output_channel,
                drawbar_cc_start_num + drawbar_index,
                drawbar_value
            );
        ) : (
          // Knobs:
          // "Overdrive" knob:
          b08_control_index == 63 ? (
              cc_num = 75;
          );
          // "Tone" knob:
          b08_control_index == 97 ? (
              cc_num = 76;
          );
          // "Compressor" knob:
          b08_control_index == 98 ? (
              cc_num = 77;
          );
          // "MFX" knob:
          b08_control_index == 94 ? (
              cc_num = 78;
          );
          // "Delay" knob:
          b08_control_index == 96 ? (
              cc_num = 79;
          );
          // "Reverb" knob:
          // TODO. This knob behaves differently than all the other knobs!
          midisend(
              offset,
              cc_status_byte + output_channel,
              cc_num,
              b09_control_val
          )
        )
    ) : (
        midisend_buf(offset,buf,recvlen); // passthrough other events
    )
);
